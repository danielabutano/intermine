apply plugin: 'war'

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

def explodedWebAppDir = "$buildDir/explodedWebApp"

configurations {
    imWebServices

    providedRuntime
    all*.exclude module : 'spring-boot-starter-logging'
    providedRuntime
    compile.exclude group:'ch.qos.logback'
}

dependencies {
    providedCompile project(':bio-core')
    providedCompile project(':bio-model')
    providedCompile project(':bio-webcore')
    providedCompile group: "org.intermine", name: "intermine-webcore", version: System.getProperty("imVersion")
    providedCompile group: "org.intermine", name: "intermine-webservices", version: System.getProperty("imVersion"), classifier: "classes"
    providedCompile group: 'org.apache.struts', name: 'struts-tiles', version: '1.3.10'
    providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.0.1'

    compile 'org.springframework.boot:spring-boot-starter-web:1.5.9.RELEASE'
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat:1.5.9.RELEASE'
    compile 'io.springfox:springfox-swagger2:2.7.0'
    compile 'io.springfox:springfox-swagger-ui:2.7.0'
    compile 'com.github.joschi.jackson:jackson-datatype-threetenbp:2.6.4'
    compile 'javax.validation:validation-api:1.1.0.Final'

    compile (project(':bio-tools')) { transitive = false }
    compile group: "org.intermine", name: "intermine-jbrowse-endpoint", version: System.getProperty("imVersion")
    compile group: 'psidev.psi.mi.jami.bridges', name: 'bridges-core', version: '1.1.0'
    compile group: 'psidev.psi.mi.jami.bridges', name: 'jami-ols', version: '1.1.0'
    compile group: 'psidev.psi.mi.jami', name: 'jami-core', version: '1.2.3'
    compile group: 'psidev.psi.mi.jami', name: 'jami-interactionviewer-json', version: '2.0.2'
    compile group: "org.biojava", name: "biojava-genome", version: "4.2.0"

    testCompile 'org.springframework.boot:spring-boot-starter-test:1.5.9.RELEASE'
    
    imWebServices group: "org.intermine", name: "intermine-webservices", version: System.getProperty("imVersion"), transitive: false
}

task unwarIMWebServices(type: Copy) {
    description "Unwar intermine-webservices under the build/explodedWebAppDir directory"
    from { zipTree(configurations.imWebServices.singleFile) }
    destinationDir = new File("$explodedWebAppDir")
}

task warBioWebServices(type: War) {
    description "Create the war from the build/explodedWebAppDir directory"
    from "${explodedWebAppDir}"
}

war {
    dependsOn 'unwarIMWebServices'
    finalizedBy 'warBioWebServices'
}

task classesJar(type: Jar) {
    classifier = 'classes'
    from sourceSets.main.output
}

artifacts {
    archives classesJar
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}